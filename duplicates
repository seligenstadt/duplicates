#!/usr/bin/perl

=head1 NAME

duplicates - a utility for searching and removing duplicates of regular files
within given folders

=head1 SYNOPSIS

B<duplicates> [--(no)recursive] [--(no)remove] [--(no)log] [--duplicates FILE] --folder A,B --folder C ... 

=head1 DEPENDENCIES

B<duplicates> uses a system utility B<find> and B<sha256sum> for digest calculation.

=head1 BUGS

If a folder name contains an apostrophe-character, then the result will be unpredictable.

=head1 AUTHOR

Roman Dieser, roman.dieser@mail.ru

=head1 COPYRIGHT

Copyright (c) 2013 by Roman Dieser. All rights reserved.

This program is free software; you can redistribute it and/or modify it under the same terms as Perl itself.

=cut
  
use strict;
use warnings;

use Getopt::Long;

my @folders    = qw();
my $recursive  = 1;
my $remove     = 0;
my $log        = 1;
my $duplicates = 0;
my %file       = qw();

GetOptions(
    "folder=s"     => \@folders,
    "recursive!"   => \$recursive,
    "remove!"      => \$remove,
    "log!"         => \$log,
    "duplicates=s" => \$duplicates,
);

@folders = split(/,/ , join(',', @folders));

unless (@folders) {
    die "no folder(s) to search in!\n";
}

if ($duplicates) {
    open(DUPLICATES, ">$duplicates") || die "can't open '$duplicates' for writing!\n";
}

if ($log) { print "\n" . '=' x 16 . ' FILES ' . '=' x 16 . "\n"; }

my($file_digest, $file_name);

foreach my $folder (@folders) {
    if ($log) { print "\nfolder:\t$folder\n"; }
    my $find_cmd = "find '$folder' -xdev";
    unless ($recursive) { $find_cmd .= ' -maxdepth 1'; }
    $find_cmd .= ' -type f -exec sha256sum -b {} \;';
    open(FILE, $find_cmd . ' |');
    while (<FILE>) {
        chomp;
        ($file_digest, $file_name) = split(/ \*/);
        if ($log) { print "\t$file_digest - $file_name\n"; }
        if (defined($file{$file_digest})) {
            push @{ $file{$file_digest} }, $file_name;
        } else {
            $file{$file_digest} = [ $file_name ];
        }
    }
    close(FILE);
}

unless ($duplicates) { print "\n" . '=' x 16 . ' DUPLICATES ' . '=' x 16 . "\n\n"; }

foreach $file_digest (keys %file) {
    my $duplicates_number = scalar @{ $file{$file_digest} };
    if ($duplicates_number > 1) {
        if ($duplicates) {
            print DUPLICATES $file_digest . ': ' . $duplicates_number . " files\n";
        } else {
            print $file_digest . ': ' . $duplicates_number . " files\n";
        }
        
        my $cnt = 0;

        foreach $file_name (@{ $file{$file_digest} }) {
            if ($duplicates) {
                print DUPLICATES "\t$file_name\n"; 
            } else {
                print "\t$file_name\n";
            }
            
            if ($remove && $cnt++) { 
                unlink $file_name || warn "could not unlink '$file_name': $!"; 
            }
        }
    }
}
